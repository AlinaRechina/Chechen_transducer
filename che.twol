Alphabet
    а б в г д е ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю Ӏ я
	%{obl%}:а %{obl%}:и %{obl%}:0
	%{obllight%}:а %{obllight%}:и %{obllight%}:0
;

Sets
		vowels = а е и у о ы;
		liquids = л р;
		truecons = б в г д ж з к м н п с т ф х ц ч ш щ ъ ь Ӏ;
		consnoj = б в г д ж з к л м н п р с т ф х ц ч ш щ ъ ь Ӏ;
		consonants = б в г д ж з к л м н п р с т ф х ц ч ш щ ъ ь Ӏ й;

Rules

"drop morpheme boundary"
%>:0 <=> _ ;

"no vowel between vowel and any consonant"
%{obl%}:0 <=> vowels _ consonants;

"no epenthesis between й and a consonant != й"
%{obl%}:0 <=> й:й _ consnoj;

"и epenthesis between any consonant and й"
%{obl%}:и <=> consonants _ й:й;

"а epenthesis between consonant != й and another consonant !=й"
%{obl%}:а <=> consnoj _ consnoj;

"no vowel between vowel and any consonant"
%{obllight%}:0 <=> vowels _ consonants;

"и epenthesis between any consonant and й"
%{obllight%}:и <=> consonants _ й:й;

"а epenthesis between consonant != й,л,р and another consonant != й,л,р"
%{obllight%}:а <=> truecons _ truecons;

"no epenthesis between a liquid and a consonant != й"
%{obllight%}:0 <=> liquids _ consnoj;

"no epenthesis between й and a consonant != й"
%{obllight%}:0 <=> й:й _ consnoj;

